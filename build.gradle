group 'pl.warp'
version '0.1.2'

apply plugin: 'java'

repositories {
    mavenCentral()
}

def platform=project.properties['platform'] ?: "windows"

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.12'
    testCompile 'junit:junit:4.12'
}

subprojects.each { subproject -> evaluationDependsOn( subproject.path ) }
jar.dependsOn subprojects.tasks['classes']
jar {
    subprojects.each { subproject ->
        from { subproject.configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    }
    def ideModule = subprojects.find { s -> s.name == "ide" }
    from {ideModule.sourceSets.main.output.classesDir}
    from {ideModule.sourceSets.main.output.resourcesDir}
}

task testJar(type: Jar, dependsOn: 'build') {
    manifest {
        attributes 'Main-Class': 'pl.warp.test.TestApp'
    }
    baseName = "warp-${platform}"
    with jar
}

task ideJar(type: Jar, dependsOn: 'build') {
    manifest {
        attributes 'Main-Class': 'pl.warp.ide.IDELauncher'
    }
    baseName = "warp-IDE-${platform}"
    with jar
}



subprojects {
    test {
        reports.junitXml.enabled = false
    }
}